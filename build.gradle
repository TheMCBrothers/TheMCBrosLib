plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0, 6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version '2.+'
}

group = "net.themcbrothers"
version = "$minecraft_version-$mod_version"

base {
    archivesName = "TheMCBrosLib"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }

    withSourcesJar()
}

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                tmcb_lib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                tmcb_lib {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'tmcb_lib', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                tmcb_lib {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

processResources {
    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': version],
                'loader_version_range': loader_version_range,
                'forge_version_range': forge_version_range,
                'minecraft_version_range': minecraft_version_range
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Mod dependencies
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title"     : "tmcb_lib",
                "Specification-Vendor"    : "TheMCBrothers",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "TheMCBrothers",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourcesJar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archiveClassifier.set('sources')
    from sourceSets.main.allJava;
}

artifacts {
    archives tasks.jar
    archives tasks.sourcesJar
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

private String getSecret(String key) {
    def secret = System.getenv(key)
    if (secret != null) {
        return secret.trim()
    }

    File secretFile = file('secrets.properties')
    if (secretFile.canRead()) {
        Properties secretProps = new Properties()
        secretProps.load(new FileInputStream(secretFile))
        return secretProps.getProperty(key).toString()
    }
    return null
}

// publishing on NEXUS
publishing {
    publications { PublicationContainer publicationContainer ->
        publicationContainer.register("maven", MavenPublication) { MavenPublication publication ->
            publication.artifact(jar)
            publication.artifact(sourcesJar)
            publication.version("$project.version")
            publication.artifactId("TheMCBrosLib")
        }
    }
    repositories {
        maven {
            credentials {
                username = getSecret('nexus_uname')
                password = getSecret('nexus_pwd')
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url 'https://nexus.themcbrothers.net/repository/maven-snapshots/'
            } else {
                url 'https://nexus.themcbrothers.net/repository/maven-releases/'
            }
        }
    }
}

var curseForgeKey = getSecret('cf_key')
var modrinthToken = getSecret('modrinth_token')

// Publish to CurseForge
if (curseForgeKey != null) {
    curseforge {
        apiKey = curseForgeKey

        project {
            id = project.curse_project
            releaseType = project.artifact_type
            addGameVersion(project.minecraft_version)
            mainArtifact(tasks.jar)
        }
    }
}

// Publish to Modrinth
if (modrinthToken != null) {
    modrinth {
        token = getSecret('cf_key')
        projectId = modrinth_project
        versionType = artifact_type
        uploadFile = jar
        additionalFiles.add(sourcesJar)
    }
}